# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2014 Yandex LLC
# All Rights Reserved.
#
# Author : Alexis Wilpert



import 'alphabets.grm' as alphabets;
import 'definitions.grm' as defs;

# approximant insertion is necessary when:
# - <e, ю, я, ё> are after another vowel
# - or after <ь, ъ>
# - or at the beginning of the word.

BOS = "[BOS]" | "[SIL]";
SEP = alphabets.word_sep;
WUD = "[WUD]" SEP;

#----------------------------------------------------------------------------

insertion_context = (alphabets.vow_letter |
                     "й"                  |
                     ("ь" | "ъ")          |
                     defs.LSEP            |
                     BOS
                    ) "+"?
;

insertion_pairs = ("е":"jе") |
                  ("ю":"jю") |
                  ("я":"jя") |
                  ("ё":"jё")
;

initial_approximant_insertion = CDRewrite[insertion_pairs,
                                          insertion_context,
                                          "",
                                          alphabets.sigma_star
                                         ];

# correct approximant insertion after WUD

approx_insertion_correction = CDRewrite[("j":""),
                                        "[WUD]" SEP,
                                        "",
                                        alphabets.sigma_star
                                       ];

approximant_insertion = Optimize[initial_approximant_insertion @
                                 approx_insertion_correction
                                ];

# WARNING: possible output --> + j V

#----------------------------------------------------------------------------

# we do the palatalization here since later we will loose the orthographic
# context triggered by <я>, <ю> and <ё>

# we need to take this in consideration in the rules below

palatal_cons_pairs = ("б":"[bJ]") |
                     ("в":"[vJ]") |
                     ("г":"[gJ]") |
                     ("д":"[dJ]") |
                     ("з":"[zJ]") |
                     ("к":"[kJ]") |
                     ("л":"[lJ]") |
                     ("м":"[mJ]") |
                     ("н":"[nJ]") |
                     ("п":"[pJ]") |
                     ("р":"[rJ]") |
                     ("с":"[sJ]") |
                     ("т":"[tJ]") |
                     ("ф":"[fJ]") |
                     ("х":"[xJ]")
;

cd_palatalization  = CDRewrite[palatal_cons_pairs,
                               "",
                               "ь" | ("-"? "+"?
                                      alphabets.soft_vow_letter),
                               alphabets.sigma_star
                              ];

#----------------------------------------------------------------------------

export palatalization = Optimize[approximant_insertion @
                                 cd_palatalization
                                ];

